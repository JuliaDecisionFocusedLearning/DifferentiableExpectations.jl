"""
    REINFORCE{threaded} <: DifferentiableExpectation{threaded}

Differentiable parametric expectation `F : Œ∏ -> ùîº[f(X)]` where `X ‚àº p(Œ∏)` using the REINFORCE (or score function) gradient estimator:
```
‚àÇF(Œ∏) = ùîº[f(X) ‚àá‚ÇÅlogp(Œ∏, x)·µÄ]
```

# Constructor

    REINFORCE(
        f,
        dist_constructor,
        dist_gradlogpdf=nothing;
        rng=Random.default_rng(),
        nb_samples=1,
        threaded=false
    )

# Fields

$(TYPEDFIELDS)

# See also

- [`DifferentiableExpectation`](@ref)
"""
struct REINFORCE{threaded,F,D,G,R<:AbstractRNG} <: DifferentiableExpectation{threaded}
    f::F
    dist_constructor::D
    dist_logdensity_grad::G
    rng::R
    nb_samples::Int
end

function REINFORCE(
    f::F,
    dist_constructor::D,
    dist_logdensity_grad::G=nothing;
    rng::R=default_rng(),
    nb_samples=1,
    threaded=false,
) where {F,D,G,R}
    return REINFORCE{threaded,F,D,G,R}(
        f, dist_constructor, dist_logdensity_grad, rng, nb_samples
    )
end

function logdensity_grad(rc::RuleConfig, F::REINFORCE{threaded}, x, Œ∏...) where {threaded}
    (; dist_constructor, dist_logdensity_grad) = F
    if !isnothing(dist_logdensity_grad)
        dŒ∏ = dist_logdensity_grad(Œ∏...)
    else
        # TODO: add Distributions.gradlogpdf
        _logdensity_partial(_Œ∏...) = logdensityof(dist_constructor(_Œ∏...), x)
        l, pullback = rrule_via_ad(rc, _logdensity_partial, Œ∏...)
        dŒ∏ = Base.tail(pullback(one(l)))
    end
    return dŒ∏
end

function logdensity_grads_from_presamples(
    rc::RuleConfig,
    F::DifferentiableExpectation{threaded},
    xs::AbstractVector,
    Œ∏...;
    kwargs...,
) where {threaded}
    _logdensity_grad_partial(x) = logdensity_grad(rc, F, x, Œ∏...)
    if threaded
        return tmap(_logdensity_grad_partial, xs)
    else
        return map(_logdensity_grad_partial, xs)
    end
end

function logdensity_grads_from_presamples(
    rc::RuleConfig,
    F::DifferentiableExpectation{threaded},
    xs::AbstractMatrix,
    Œ∏...;
    kwargs...,
) where {threaded}
    _logdensity_grad_partial(x) = logdensity_grad(rc, F, x, Œ∏...)
    if threaded
        return tmap(_logdensity_grad_partial, eachcol(xs))
    else
        return map(_logdensity_grad_partial, eachcol(xs))
    end
end

function ChainRulesCore.rrule(
    rc::RuleConfig, F::REINFORCE{threaded}, Œ∏...; kwargs...
) where {threaded}
    project_Œ∏ = ProjectTo(Œ∏)

    (; nb_samples) = F
    xs = presamples(F, Œ∏...)
    ys = samples_from_presamples(F, xs; kwargs...)
    gs = logdensity_grads_from_presamples(rc, F, xs, Œ∏...)

    function REINFORCE_pullback(dy_thunked)
        dy = unthunk(dy_thunked)
        dF = @not_implemented(
            "The fields of the `REINFORCE` object are considered constant."
        )
        _single_sample_pullback(g, y) = g .* dot(y, dy)
        dŒ∏ = if threaded
            tmapreduce(_single_sample_pullback, .+, gs, ys) ./ nb_samples
        else
            mapreduce(_single_sample_pullback, .+, gs, ys) ./ nb_samples
        end
        dŒ∏_proj = project_Œ∏(dŒ∏)
        return (dF, dŒ∏_proj...)
    end

    y = threaded ? tmean(ys) : mean(ys)
    return y, REINFORCE_pullback
end
