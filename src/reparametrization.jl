struct TransformedDistribution{D,T}
    base_dist::D
    transformation::T
end

function reparametrize(dist::Normal{T}) where {T}
    base_dist = Normal(zero(T), one(T))
    Î¼, Ïƒ = mean(dist), std(dist)
    transformation(z) = Î¼ + Ïƒ * z
    return TransformedDistribution(base_dist, transformation)
end

"""
    Reparametrization{threaded} <: DifferentiableExpectation{threaded}

Differentiable parametric expectation `F : Î¸ -> ğ”¼[f(X)]` where `X âˆ¼ p(Î¸)` using the reparametrization (or pathwise) gradient estimator: if `X = g(Z,Î¸)` where `Z âˆ¼ q` then
```
âˆ‚F(Î¸) = ğ”¼_q[âˆ‚f(g(Z,Î¸)) âˆ‚â‚‚g(Z,Î¸)áµ€]
```

# Example

```jldoctest
using DifferentiableExpectations, Distributions, Zygote

F = Reparametrization(exp, Normal; nb_samples=10^3)
F_true(Î¼, Ïƒ) = mean(LogNormal(Î¼, Ïƒ))

Î¼, Ïƒ = 0.5, 1,0
âˆ‡F, âˆ‡F_true = gradient(F, Î¼, Ïƒ), gradient(F_true, Î¼, Ïƒ)
isapprox(collect(âˆ‡F), collect(âˆ‡F_true); rtol=1e-1)

# output

true
```

# Constructor

    Reparametrization(
        f,
        dist_constructor,
        rng=Random.default_rng(),
        nb_samples=1,
        threaded=false
    )

# Fields

$(TYPEDFIELDS)

# See also

- [`DifferentiableExpectation`](@ref)
"""
struct Reparametrization{threaded,F,D,R<:AbstractRNG} <: DifferentiableExpectation{threaded}
    f::F
    dist_constructor::D
    rng::R
    nb_samples::Int
end

function Base.show(io::IO, rep::Reparametrization{threaded}) where {threaded}
    (; f, dist_constructor, rng, nb_samples) = rep
    return print(
        io, "Reparametrization{$threaded}($f, $dist_constructor, $rng, $nb_samples)"
    )
end

function Reparametrization(
    f::F, dist_constructor::D; rng::R=default_rng(), nb_samples=1, threaded=false
) where {F,D,R}
    return Reparametrization{threaded,F,D,R}(f, dist_constructor, rng, nb_samples)
end

function ChainRulesCore.rrule(
    rc::RuleConfig, F::Reparametrization{threaded}, Î¸...; kwargs...
) where {threaded}
    project_Î¸ = ProjectTo(Î¸)

    (; f, dist_constructor, rng, nb_samples) = F
    dist = dist_constructor(Î¸...)
    transformed_dist = reparametrize(dist)
    zs = rand(rng, transformed_dist.base_dist, nb_samples)
    xs = transformed_dist.transformation.(zs)
    ys = samples_from_presamples(F, xs; kwargs...)

    function h(z, Î¸)
        transformed_dist = reparametrize(dist_constructor(Î¸...))
        return f(transformed_dist.transformation(z); kwargs...)
    end

    function pullback_Reparametrization(dy_thunked)
        dy = unthunk(dy_thunked)
        dF = @not_implemented(
            "The fields of the `Reparametrization` object are considered constant."
        )
        function _single_sample_pullback(z)
            _, pb = rrule_via_ad(rc, h, z, Î¸)
            _, _, dÎ¸ = pb(dy)
            return dÎ¸
        end
        dÎ¸ = if threaded
            tmapreduce(_single_sample_pullback, .+, zs) ./ nb_samples
        else
            mapreduce(_single_sample_pullback, .+, zs) ./ nb_samples
        end
        dÎ¸_proj = project_Î¸(dÎ¸)
        return (dF, dÎ¸_proj...)
    end

    y = threaded ? tmean(ys) : mean(ys)
    return y, pullback_Reparametrization
end
