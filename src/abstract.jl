"""
    DifferentiableExpectation{threaded}

Abstract supertype for differentiable parametric expectations `F : Œ∏ -> ùîº[f(X)]` where `X ‚àº p(Œ∏)`, whose value and derivative are approximated with Monte-Carlo averages.

# Type parameters

  - `threaded::Bool`: specifies whether the sampling should be performed in parallel (with OhMyThreads.jl)

# Required fields

  - `f`: the function applied inside the expectation
  - `dist_constructor`: the constructor of the probability distribution, such that calling `D(Œ∏...)` generates an object corresponding to `p(Œ∏)`
  - `rng`: the random number generator
  - `nb_samples`: the number of Monte-Carlo samples
"""
abstract type DifferentiableExpectation{threaded} end

"""
    presamples(F::DifferentiableExpectation, Œ∏...)

Return a vector `[x‚ÇÅ, ..., x‚Çõ]` or matrix `[x‚ÇÅ ... x‚Çõ]` where the `x·µ¢ ‚àº p(Œ∏)` are iid samples.
"""
function presamples(F::DifferentiableExpectation, Œ∏...)
    (; dist_constructor, rng, nb_samples) = F
    dist = dist_constructor(Œ∏...)
    xs = rand(rng, dist, nb_samples)  # TODO: parallelize?
    return xs
end

"""
    samples(F::DifferentiableExpectation, Œ∏...; kwargs...)

Return a vector `[f(x‚ÇÅ), ..., f(x‚Çõ)]` where the `x·µ¢ ‚àº p(Œ∏)` are iid samples.
"""
function samples(F::DifferentiableExpectation{threaded}, Œ∏...; kwargs...) where {threaded}
    xs = presamples(F, Œ∏...)
    return samples_from_presamples(F, xs; kwargs...)
end

function samples_from_presamples(
    F::DifferentiableExpectation{threaded}, xs::AbstractVector; kwargs...
) where {threaded}
    (; f) = F
    fk = FixKwargs(f, kwargs)
    if threaded
        return tmap(fk, xs)
    else
        return map(fk, xs)
    end
end

function samples_from_presamples(
    F::DifferentiableExpectation{threaded}, xs::AbstractMatrix; kwargs...
) where {threaded}
    (; f) = F
    fk = FixKwargs(f, kwargs)
    if threaded
        return tmap(fk, eachcol(xs))
    else
        return map(fk, eachcol(xs))
    end
end

"""
    (F::DifferentiableExpectation)(Œ∏...; kwargs...)

Return a Monte-Carlo average `(1/s) ‚àëf(x·µ¢)` where the `x·µ¢ ‚àº p(Œ∏)` are iid samples.
"""
function (F::DifferentiableExpectation{threaded})(Œ∏...; kwargs...) where {threaded}
    ys = samples(F, Œ∏...; kwargs...)
    y = if threaded
        tmean(ys)
    else
        mean(ys)
    end
    return y
end
